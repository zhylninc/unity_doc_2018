<html><head><title>html模版</title></head><body>Unity - Manual: Rendering Optimizations</br>
<div class="breadcrumbs clear"><ul>
<li><a href="UnityManual.html">Unity User Manual (2018.1 beta)</a></li>
<li><a href="PlatformSpecific.html">Platform-specific</a></li>
<li><a href="MobileDeveloperChecklist.html">Mobile Developer Checklist</a></li>
<li><a href="MobileOptimizationPracticalGuide.html"> Practical guide to optimization for mobiles</a></li>
<li>Rendering Optimizations</li>
</ul></div>
<div class="mb20"><div class="nextprev clear">
<div class="icon tt left mr1" data-distance="-40|-30|top">
<span class="prev"><a href="MobileOptimizationScriptingMethods.html"></a></span><div class="tip">Scripting and Gameplay Methods</div>
</div>
<div class="icon tt right" data-distance="-40|-30|top">
<span class="next"><a href="MobileOptimizationPracticalScriptingOptimizations.html"></a></span><div class="tip">Optimizing Scripts</div>
</div>
</div></div>
<div class="otherversionswrapper" onmouseover="setOtherVersionsDisplay(true)" onmouseout="setOtherVersionsDisplay(false)">
<a>Other Versions</a><div class="otherversionscontent" id="OtherVersionsContent" style="display: none;">Cannot access other versions offline!</div>
</div>
<div class="scrollToFeedback"><a id="scrollToFeedback">Leave feedback</a></div>
<h1>Rendering Optimizations</h1>
<!--BeginSwitchLink--><!--EndSwitchLink-->
<div class="clear"></div>

<p>This section introduces the technicalities of rendering optimization. It shows how to bake lighting results for better performance, and how the developers of Shadowgun levered high-contrast textures, with lighting baked-in, to make their game look great. If you are looking for general information on what a mobile-optimized game looks like, check out the <a href="MobileOptimizationGraphicsMethods.html">Graphics Methods</a> page.</p>

<h2>Get Artsy!</h2>

<p>Sometimes optimizing the rendering in your game requires some dirty work. All of the structure that Unity provides makes it easy to get something working fast, but if you require top notch fidelity on limited hardware, doing things yourself and sidestepping that structure is the way to go, provided that you can introduce a key structural change that makes things a lot faster. Your tools of choice are editor scripts, simple shaders, and good old-fashioned art production.</p>

<h3>How to Dive Under the Hood</h3>

<p>First of all, check out this <a href="Shaders.html">introduction to how shaders are written</a>.</p>

<ul>
<li>
<a href="http://unity3d.com/support/resources/assets/built-in-shaders">Built in shaders</a>

<ul>
<li>Examine the source code of the built in shaders. Often, if you want to make a new shader that does something different, you can achieve it by taking parts of two already-existing shaders and putting them together.</li>
</ul>
</li>
<li>Surface Shader Debugging (<code>#pragma debug</code>)

<ul>
<li>A CG Shader is generated from every surface shader, and then fully compiled from there. If you add <code>#pragma debug</code> to the top of your surface shader, when you open the compiled shader via the inspector, you can see the intermediate CG code. This is useful for inspecting how a specific part of a shader is actually calculated, and it can also be useful for grabbing certain aspects you want from a surface shader and applying them to a CG shader.</li>
</ul>
</li>
<li>Shader Include Files

<ul>
<li>A lot of shader helper code is included in every shader, and usually it isn’t used, but this is why you will sometimes see shaders calling functions like WorldReflectionVector which don’t seem to be defined anywhere. Unity may has several built-in shader include files that contain these helper definitions. To find a specific function, you will need to search through all of the different includes.</li>
<li>These files are a major part of internal structure that Unity uses to make it easy to write shaders; the files provide things like real time shadows, different light types, lightmaps, and multiple platform support.</li>
</ul>
</li>
<li>Hardware documentation

<ul>
<li>Take your time to study Apple documentation on <a href="http://developer.apple.com/library/ios/#documentation/3DDrawing/Conceptual/OpenGLES_ProgrammingGuide/BestPracticesforShaders/BestPracticesforShaders.html">best practices for writing shaders</a>. Note that we would suggest to be more aggressive with floating point precision hints however.</li>
</ul>
</li>
</ul>

<h2>
<a href="http://blogs.unity3d.com/2012/03/23/shadowgun-optimizing-for-mobile-sample-level/">Shadowgun</a> in-depth</h2>

<p>Shadowgun is a great graphical achievement considering the hardware it runs on. While the art quality seems to be the key to the puzzle, there are a couple tricks to achieving such quality that programmers can pull off to maximize their artists’ potential.</p>

<p>In the <a href="MobileOptimizationGraphicsMethods.html">Graphics Methods</a> page we used the golden statue in Shadowgun as an example of a great optimization; instead of using a normal map to give their statue some solid definition, they just baked lighting detail into the texture. Here, we will show you how and why you should use a similar technique in your own game.</p>

<h3>Shader code for Real-Time vs Baked Golden Statue</h3>

<pre><code>// This is the pixel shader code for drawing normal-mapped
// specular highlights on static lightmapped geometry

// 5 texture reads, lots of instructions

SurfaceOutput o;

fixed4 tex = tex2D(_MainTex, IN.uv_MainTex);
fixed4 c = tex * _Color;
o.Albedo = c.rgb;

o.Gloss = tex.a;
o.Specular = _Shininess;

o.Normal = UnpackNormal(tex2D(_BumpMap, IN.uv_BumpMap));

float3 worldRefl = WorldReflectionVector (IN, o.Normal);
fixed4 reflcol = texCUBE (_Cube, worldRefl);
reflcol *= tex.a;
o.Emission = reflcol.rgb * _ReflectColor.rgb;
o.Alpha = reflcol.a * _ReflectColor.a;

fixed atten = LIGHT_ATTENUATION(IN);
fixed4 c = 0;

half3 specColor;
fixed4 lmtex = tex2D(unity_Lightmap, IN.lmap.xy);
fixed4 lmIndTex = tex2D(unity_LightmapInd, IN.lmap.xy);

const float3x3 unity_DirBasis = float3x3( 
float3( 0.81649658,  0.0, 0.57735028),
float3(-0.40824830,  0.70710679, 0.57735027),
float3(-0.40824829, -0.70710678, 0.57735026) );

half3 lm = DecodeLightmap (lmtex);

half3 scalePerBasisVector = DecodeLightmap (lmIndTex);

half3 normalInRnmBasis = saturate (mul (unity_DirBasis, o.Normal));
lm *= dot (normalInRnmBasis, scalePerBasisVector);

return half4(lm, 1);
</code></pre>

<pre><code>// This is the pixel shader code for lighting which is
// baked into the texture

// 2 texture reads, very few instructions

fixed4 c = tex2D (_MainTex, i.uv.xy);   

c.xyz += texCUBE(_EnvTex,i.refl) * _ReflectionColor * c.a;

return c;
</code></pre>

<p>
<img src="../uploads/Main/MobileOptimizationPracticalRenderingOptimizations-0.gif" alt="Reflective Bumped Specular">
<img src="../uploads/Main/MobileOptimizationPracticalRenderingOptimizations-1.jpg" alt="">
<img src="../uploads/Main/MobileOptimizationPracticalRenderingOptimizations-2.gif" alt="Baked Light with Reflection">
<img src="../uploads/Main/MobileOptimizationPracticalRenderingOptimizations-3.jpg" alt="">
</p>

<h3>Render to Texel</h3>

<p>The real-time light is definitely higher quality, but the performance gain from the baked version is massive. So how was this done? An editor tool called Render to Texel was created for this purpose. It bakes the light into the texture through the following process:</p>

<ul>
<li>Transform the tangent space normal map to world space via script.</li>
<li>Create a world space position map via script.</li>
<li>Render to Texture a fullscreen pass of a the entire texture using the two previous maps, with one additional pass per light.</li>
<li>Average results from several different vantage points to yield something which looks plausible all around, or at least from common viewing angles in your game.</li>
</ul>

<p>This is how the best graphics optimizations work. They sidestep tons of calculations by preforming them in the editor or before the game runs. In general, this is what you want to do:</p>

<figure>
<img src="../uploads/Main/MobileOptimizationPracticalRenderingOptimizations-4.jpg" alt="">
</figure>

<ul>
<li>Create something that looks great, don’t worry about performance.</li>
<li>Use tools like Unity’s lightmapper and editor extensions like Render to Texel and Sprite Packer to bake it down to something which is very simple to render.

<ul>
<li>Making your own tools is the best way to do this, you can create the perfect tool suited for whatever problem your game presents.</li>
</ul>
</li>
<li>Create shaders and scripts which modulate your baked output to give it some sort of “shine”; an eye-catching effect to create an illusion of dynamic light.</li>
</ul>

<h3>Concept of Light Frequency</h3>

<figure>
<img src="../uploads/Main/MobileOptimizationPracticalRenderingOptimizations-5.jpg" alt="">
</figure>

<p>Just like the Bass and Treble of an audio track, images also have high-frequency and low-frequency components, and when you’re rendering, it’s best to handle them in different ways, similar to how stereos use subwoofers and tweeters to produce a full body of sound. One way to visualize the different frequencies of an image is to use the “High Pass” filter in Photoshop. <strong>Filters-&gt;Other-&gt;High Pass</strong>. If you have done audio work before, you will recognize the name High Pass. Essentially what it does is cut off all frequencies lower than X, the parameter you pass to the filter. For images, Gaussian Blur is the equivalent of a Low Pass.</p>

<p>This applies to realtime graphics because frequency is a good way to separate things out and determine how to handle what. For example, in a basic lightmapped environment, the final image is obtained by composite of the lightmap, which is low frequency, and the textures, which are high-frequency. In Shadowgun, low frequency light is applied to characters quickly with light probes, high frequency light is faked through the use of a simple bumpmapped shader with an arbitrary light direction.</p>

<p>In general, by using different methods to render different frequencies of light, for example, baked vs dynamic, per-object vs per-level, per pixel vs per-vertex, etc, you can create full bodied images on limited hardware. Stylistic choices aside, it’s generally a good idea to try to have strong variation colors or values at both high and low frequencies.</p>

<h3>Frequency in Practice: Shadowgun Decomposition</h3>

<figure>
<img src="../uploads/Main/MobileOptimizationPracticalRenderingOptimizations-6.jpg" alt="">
</figure>

<ul>
<li>Top Row

<ul>
<li>Ultra-Low-Frequency Specular Vertex Light (Dynamic) | High Frequency Alpha Channel | Low Frequency Lightmap | High Frequency Albedo</li>
</ul>
</li>
<li>Mid Row

<ul>
<li>Specular Vertex Light * Alpha | High Frequency Additive Details | Lightmap * Color Channel</li>
</ul>
</li>
<li>Bottom

<ul>
<li>Final Sum</li>
</ul>
</li>
</ul>

<p>
<strong>Note:</strong> Usually these decompositions refer to steps in a deferred renderer, but that’s not the case here. All of this is done in just one pass. These are the two relevant shaders which this composition was based on:</p>

<h4>Lightmapped with Virtual Gloss Per-Vertex Additive</h4>

<pre><code>Shader &quot;MADFINGER/Environment/Virtual Gloss Per-Vertex Additive (Supports Lightmap)&quot; {
Properties {
    _MainTex (&quot;Base (RGB) Gloss (A)&quot;, 2D) = &quot;white&quot; {}
    //_MainTexMipBias (&quot;Base Sharpness&quot;, Range (-10, 10)) = 0.0
    _SpecOffset (&quot;Specular Offset from Camera&quot;, Vector) = (1, 10, 2, 0)
    _SpecRange (&quot;Specular Range&quot;, Float) = 20
    _SpecColor (&quot;Specular Color&quot;, Color) = (0.5, 0.5, 0.5, 1)
    _Shininess (&quot;Shininess&quot;, Range (0.01, 1)) = 0.078125
    _ScrollingSpeed(&quot;Scrolling speed&quot;, Vector) = (0,0,0,0)
}

SubShader {
    Tags { &quot;RenderType&quot;=&quot;Opaque&quot; &quot;LightMode&quot;=&quot;ForwardBase&quot;}
    LOD 100



    CGINCLUDE
    #include &quot;UnityCG.cginc&quot;
    sampler2D _MainTex;
    float4 _MainTex_ST;
    samplerCUBE _ReflTex;

    #ifdef LIGHTMAP_ON
    float4 unity_LightmapST;
    sampler2D unity_Lightmap;
    #endif

    //float _MainTexMipBias;
    float3 _SpecOffset;
    float _SpecRange;
    float3 _SpecColor;
    float _Shininess;
    float4 _ScrollingSpeed;

    struct v2f {
        float4 pos : SV_POSITION;
        float2 uv : TEXCOORD0;
        #ifdef LIGHTMAP_ON
        float2 lmap : TEXCOORD1;
        #endif
        fixed3 spec : TEXCOORD2;
    };


    v2f vert (appdata_full v)
    {
        v2f o;
        o.pos = UnityObjectToClipPos(v.vertex);

        o.uv = v.texcoord + frac(_ScrollingSpeed * _Time.y);

        float3 viewNormal = UnityObjectToViewPos(v.normal);
        float3 viewPos = UnityObjectToViewPos(v.vertex);
        float3 viewDir = float3(0,0,1);
        float3 viewLightPos = _SpecOffset * float3(1,1,-1);

        float3 dirToLight = viewPos - viewLightPos;

        float3 h = (viewDir + normalize(-dirToLight)) * 0.5;
        float atten = 1.0 - saturate(length(dirToLight) / _SpecRange);

        o.spec = _SpecColor * pow(saturate(dot(viewNormal, normalize(h))), _Shininess * 128) * 2 * atten;

        #ifdef LIGHTMAP_ON
        o.lmap = v.texcoord1.xy * unity_LightmapST.xy + unity_LightmapST.zw;
        #endif
        return o;
    }
    ENDCG


    Pass {
        CGPROGRAM
        #pragma vertex vert
        #pragma fragment frag
        fixed4 frag (v2f i) : SV_Target
        {
            fixed4 c = tex2D (_MainTex, i.uv);

            fixed3 spec = i.spec.rgb * c.a;

            #if 1
            c.rgb += spec;
            #else           
            c.rgb = c.rgb + spec - c.rgb * spec;
            #endif

            #ifdef LIGHTMAP_ON
            fixed3 lm = DecodeLightmap (tex2D(unity_Lightmap, i.lmap));
            c.rgb *= lm;
            #endif

            return c;
        }
        ENDCG 
    }   
}
}

</code></pre>

<h3>Lightprobes with Virtual Gloss Per-Vertex Additive</h3>

<pre><code>Shader &quot;MADFINGER/Environment/Lightprobes with VirtualGloss Per-Vertex Additive&quot; {
Properties {
    _MainTex (&quot;Base (RGB) Gloss (A)&quot;, 2D) = &quot;white&quot; {}
    _SpecOffset (&quot;Specular Offset from Camera&quot;, Vector) = (1, 10, 2, 0)
    _SpecRange (&quot;Specular Range&quot;, Float) = 20
    _SpecColor (&quot;Specular Color&quot;, Color) = (1, 1, 1, 1)
    _Shininess (&quot;Shininess&quot;, Range (0.01, 1)) = 0.078125    
    _SHLightingScale(&quot;LightProbe influence scale&quot;,float) = 1
}

SubShader {
    Tags { &quot;RenderType&quot;=&quot;Opaque&quot; &quot;LightMode&quot;=&quot;ForwardBase&quot;}
    LOD 100



    CGINCLUDE
    #pragma multi_compile _ LIGHTMAP_ON
    #include &quot;UnityCG.cginc&quot;
    sampler2D _MainTex;
    float4 _MainTex_ST;


    float3 _SpecOffset;
    float _SpecRange;
    float3 _SpecColor;
    float _Shininess;
    float _SHLightingScale;

    struct v2f {
        float4 pos : SV_POSITION;
        float2 uv : TEXCOORD0;
        float3 refl : TEXCOORD1;
        fixed3 spec : TEXCOORD3;
        fixed3 SHLighting: TEXCOORD4;
    };


    v2f vert (appdata_full v)
    {
        v2f o;
        o.pos = UnityObjectToClipPos(v.vertex);
        o.uv = v.texcoord;

        float3 worldNormal = UnityObjectToWorldDir(v.normal);       
        float3 viewNormal = UnityObjectToViewPos(v.normal);
        float4 viewPos = UnityObjectToViewPos(v.vertex);
        float3 viewDir = float3(0,0,1);
        float3 viewLightPos = _SpecOffset * float3(1,1,-1);

        float3 dirToLight = viewPos.xyz - viewLightPos;

        float3 h = (viewDir + normalize(-dirToLight)) * 0.5;
        float atten = 1.0 - saturate(length(dirToLight) / _SpecRange);

        o.spec = _SpecColor * pow(saturate(dot(viewNormal, normalize(h))), _Shininess * 128) * 2 * atten;

        o.SHLighting    = ShadeSH9(float4(worldNormal,1)) * _SHLightingScale;

        return o;
    }
    ENDCG


    Pass {
        CGPROGRAM
        #pragma vertex vert
        #pragma fragment frag
        fixed4 frag (v2f i) : SV_Target
        {
            fixed4 c    = tex2D (_MainTex, i.uv);

            c.rgb *= i.SHLighting;
            c.rgb += i.spec.rgb * c.a;

            return c;
        }
        ENDCG 
    }   
}
}
</code></pre>

<h2>Best Practices</h2>

<h3>GPU optimization: Alpha-Testing</h3>

<p>Some GPUs, particularly ones found in mobile devices, incur a high performance overhead for alpha-testing (or use of the <strong>discard</strong> and <strong>clip</strong> operations in pixel shaders). You should replace alpha-test shaders with alpha-blended ones if possible. Where alpha-testing cannot be avoided, you should keep the overall number of visible alpha-tested pixels to a minimum.</p>

<h3>iOS Texture Compression</h3>

<p>Some images, especially if using iOS/Android PVR texture compression, are prone to visual artifacts in the alpha channel. In such cases, you might need to tweak the PVRT compression parameters directly in your imaging software. You can do that by installing the <strong>PVR export plugin</strong> or using <a href="http://www.imgtec.com/powervr/insider/powervr-pvrtextool.asp">PVRTexTool</a> from Imagination Tech, the creators of the PVRTC format. The resulting compressed image file with a <strong>.pvr</strong> extension will be imported by the Unity editor directly and the specified compression parameters will be preserved. If PVRT-compressed textures do not give good enough visual quality or you need especially crisp imaging (as you might for GUI textures) then you should consider using 16-bit textures instead of 32-bit. By doing so, you will reduce the memory bandwidth and storage requirements by half.</p>

<h3>Android Texture Compression</h3>

<p>All Android devices with support for OpenGL ES 2.0 also support the <a href="../ScriptReference/MobileTextureSubtarget.ETC.html">ETC1 compression format</a>; it’s therefore encouraged to whenever possible use ETC1 as the prefered texture format.</p>

<p>If targeting a specific graphics architecture, such as the Nvidia Tegra or Qualcomm Snapdragon, it may be worth considering using the proprietary compression formats available on those architectures. The Android Market also allows filtering based on supported texture compression format, meaning a distribution archive (.apk) with for example <a href="class-Texture3D.html">DXT compressed textures</a> can be prevented for download on a device which doesn’t support it.</p>

<h3>An Exercise</h3>

<p>Download <a href="http://u3d.as/content/forest-johnson/render-to-texel-baker/3fD">Render to Texel</a>.
Bake lighting on your model.
Run the High Pass filter on the result in Photoshop.
Edit the “Mobile/Cubemapped” shader, included in the Render to Texel package, so that the missing low-frequency light details are replaced by vertex light.</p>
<div class="feedbackbox" id="feedbackbox">
<div id="rating"><p>Did you find this page useful? Please give it a rating:<br><div id="ratecontent" class="c-rating"></div>
</p></div>
<div id="ratingThanks" style="display:none"><p>Thanks for rating this page!</p></div>
<div id="problem"><p><a name="problem" onclick="document.getElementById('problemType').style.display = '';document.getElementById('problem').style.display = 'none';">Report a problem on this page</a></p></div>
<div id="problemType" style="display:none"><p>What kind of problem would you like to report?<ul type="problems">
<li><a name="needcode" onclick="                                                             document.getElementById('problemType').style.display = 'none';                                                             document.getElementById('problemThanks').style.display = '';                                                             SendFeedback('problem-needcode',window.location.href,0);                                                             document.getElementById('problemFormDescription').value='Needs code samples.';                                                             document.getElementById('problemNeedCodeForm').style.display = ''                                                         ">This page needs code samples</a></li>
<li><a name="code" onclick="document.getElementById('problemType').style.display = 'none';                                                         document.getElementById('problemThanks').style.display = '';                                                         SendFeedback('problem-brokencode',window.location.href,0);                                                         document.getElementById('problemFormDescription').value='Code samples do not work.';                                                         document.getElementById('problemCodeForm').style.display = ''                                                         ">Code samples do not work</a></li>
<li><a name="missing" onclick="document.getElementById('problemType').style.display = 'none';                                                         document.getElementById('problemThanks').style.display = '';                                                         SendFeedback('problem-missing',window.location.href,0);                                                         document.getElementById('problemFormDescription').value='Missing information';                                                         document.getElementById('problemMissingForm').style.display = ''                                                         ">Information is missing</a></li>
<li><a name="incorrect" onclick="document.getElementById('problemType').style.display = 'none';                                                         document.getElementById('problemThanks').style.display = '';                                                         SendFeedback('problem-incorrect',window.location.href,0);                                                         document.getElementById('problemFormDescription').value='Incorrect information';                                                         document.getElementById('problemIncorrectForm').style.display = ''                                                         ">Information is incorrect</a></li>
<li><a name="unclear" onclick="document.getElementById('problemType').style.display = 'none';                                                         document.getElementById('problemThanks').style.display = '';                                                         SendFeedback('problem-unclear',window.location.href,0);                                                         document.getElementById('problemFormDescription').value='Unclear or confusing information';                                                         document.getElementById('problemUnclearForm').style.display = ''                                                         ">Information is unclear or confusing</a></li>
<li><a name="language" onclick="document.getElementById('problemType').style.display = 'none';                                                         document.getElementById('problemThanks').style.display = '';                                                         SendFeedback('problem-language',window.location.href,0);                                                         document.getElementById('problemFormDescription').value='Spelling or grammar error';                                                         document.getElementById('problemLanguageForm').style.display = ''                                                         ">There is a spelling/grammar error on this page</a></li>
<li><a name="other" onclick="document.getElementById('problemType').style.display = 'none';                                                         document.getElementById('problemThanks').style.display = '';                                                         SendFeedback('problem-other',window.location.href,0);                                                         document.getElementById('problemFormDescription').value='This page has a problem';                                                         document.getElementById('problemOtherForm').style.display = ''                                                         ">Something else</a></li>
</ul>
<p><known_issues><p>Is something described here not working as you expect it to? It might be a <b>Known Issue</b>. Please check with the Issue Tracker at <a href="https://issuetracker.unity3d.com">issuetracker.unity3d.com</a>.</p></known_issues></p>
</p></div>
<div id="problemThanks" style="display:none"><p>Thanks for letting us know! This page has been marked for review based on your feedback.<br><br>If you have time, you can provide more information to help us fix the problem faster.<br><br><a onclick="document.getElementById('problemMoreInfo').style.display = '';document.getElementById('problemThanks').style.display = 'none';">Provide more information</a><br>
</p></div>
<div id="problemMoreInfo" style="display:none">
<p id="problemNeedCodeForm" style="display:none">You've told us this page needs code samples. If you'd like to help us further, you could provide a code sample, or tell us about what kind of code sample you'd like to see:</p>
<p id="problemCodeForm" style="display:none">You've told us there are code samples on this page which don't work. If you know how to fix it, or have something better we could use instead, please let us know:</p>
<p id="problemMissingForm" style="display:none">You've told us there is information missing from this page. Please tell us more about what's missing:</p>
<p id="problemIncorrectForm" style="display:none">You've told us there is incorrect information on this page. If you know what we should change to make it correct, please tell us:</p>
<p id="problemUnclearForm" style="display:none">You've told us this page has unclear or confusing information. Please tell us more about what you found unclear or confusing, or let us know how we could make it clearer:</p>
<p id="problemLanguageForm" style="display:none">You've told us there is a spelling or grammar error on this page. Please tell us what's wrong:</p>
<p id="problemOtherForm" style="display:none">You've told us this page has a problem. Please tell us more about what's wrong:</p>
<form>
<textarea id="problemFormSuggestionField" cols="40" rows="5"></textarea><input type="hidden" id="problemFormDescription"><input type="submit" id="problemFormDescriptionSubmit" value="Submit" onclick="                                             document.getElementById('problemMoreInfo').style.display = 'none';                                             document.getElementById('problemMoreInfoThanks').style.display = '';                                             return false;                                             ">
</form>
</div>
<div id="problemMoreInfoThanks" style="display:none"><p>Thanks for helping to make the Unity documentation better!</p></div>
<script>

                                            var ratecontent = document.querySelector('#ratecontent'); // target element
                                            var currentRating = 0; // initial rating
                                            var maxRating= 5; // max rating

                                            // callback to run after setting the rating
                                            var callback = function(rating) {
                                                document.getElementById('ratingThanks').style.display = '';
                                                document.getElementById('ratecontent').style.display = 'none';
                                                document.getElementById('rating').style.display = 'none';
                                                SendFeedback("rating",window.location.href,rating);
                                            };

                                            // rating instance
                                            var myRating = rating(ratecontent, currentRating, maxRating, callback);
                                        </script>
</div>
<div class="nextprev clear">
<div class="icon tt left mr1" data-distance="-40|-30|top">
<span class="prev"><a href="MobileOptimizationScriptingMethods.html"></a></span><div class="tip">Scripting and Gameplay Methods</div>
</div>
<div class="icon tt right" data-distance="-40|-30|top">
<span class="next"><a href="MobileOptimizationPracticalScriptingOptimizations.html"></a></span><div class="tip">Optimizing Scripts</div>
</div>
</div>
</div>
</body></html>